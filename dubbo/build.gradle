buildscript {
    ext {
        springBootVersion = "1.5.7.RELEASE"
    }

    repositories {
        mavenLocal()
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public"
        }
    }
    
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

allprojects {
    group "com.bing"
    version "1.0-SNAPSHOT"
}

subprojects {
    apply plugin: "java"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public"
        }
    }
    
    dependencies {
        compile "org.projectlombok:lombok:1.16.10"
    }

    configurations {
        all*.exclude module: "spring-boot-starter-logging"
        all*.exclude module: "slf4j-log4j12"
    }

    task createDirs {
        doLast {
            sourceSets*.java.srcDirs*.each { it.mkdirs() }
            sourceSets*.resources.srcDirs*.each { it.mkdirs()}
        }
    }
}

project(":dubbo-service") {
    apply plugin: "org.springframework.boot"

    dependencies {
        compile ("com.alibaba:dubbo:2.5.3") {
            exclude module: "spring"
        }

        compile project(":dubbo-api")

        testCompile "org.springframework.boot:spring-boot-starter-test"
    }
}

project(":dubbo-api") {
    apply plugin: "maven"
}

task zip(type: Zip){
    archiveName "test.zip"
    println project.rootDir
    from project.rootDir
}